модуль Snake

	процедура Define
	{
		# Объявления при загрузке модуля
		создавать ?что (=константа ?что иметь ?что имя ?какой %НАПРАВЛЕНИЕ_ВНИЗ).
		устанавливать ?что значение (?чего константа, ?какой 1).
		#
		создавать ?что (=константа ?что иметь ?что имя ?какой %НАПРАВЛЕНИЕ_ВВЕРХ).
		устанавливать ?что значение (?чего константа, ?какой 2).
		#
		создавать ?что (=константа ?что иметь ?что имя ?какой %НАПРАВЛЕНИЕ_ВЛЕВО).
		устанавливать ?что значение (?чего константа, ?какой 3).
		#
		создавать ?что (=константа ?что иметь ?что имя ?какой %НАПРАВЛЕНИЕ_ВПРАВО).
		устанавливать ?что значение (?чего константа, ?какой 4).
		#
		создавать ?что (=список ?что иметь ?что имя ?какой %координаты_X).
		добавлять ?что элемент ?чего список.
		устанавливать ?что значение (?чего элемент, ?какой 1).
		добавлять ?что элемент ?чего список.
		устанавливать ?что значение (?чего элемент, ?какой 1).
		добавлять ?что элемент ?чего список.
		устанавливать ?что значение (?чего элемент, ?какой 1).
		#
		создавать ?что (=список ?что иметь ?что имя ?какой %координаты_Y).
		добавлять ?что элемент ?чего список.
		устанавливать ?что значение (?чего элемент, ?какой 1).
		добавлять ?что элемент ?чего список.
		устанавливать ?что значение (?чего элемент, ?какой 2).
		добавлять ?что элемент ?чего список.
		устанавливать ?что значение (?чего элемент, ?какой 3).
		#
		создавать ?что (=поле ?что иметь ?что имя ?какой %Temp).
		#
		создавать ?что (=поле ?что иметь ?что имя ?какой %координатный_итератор).
		#
		# Вызов временной процедуры
		#выполнять ?что (=процедура ?что иметь ?что имя ?какой Snake.Temp1).
		#
		создавать ?что (=условие ?что иметь ?что имя ?какой %C1).
		устанавливать ?что обработчик (?чего условие, ?какой Snake.СP001).
		устанавливать ?что приоритет (?чего условие, ?какой 3).
		#
		создавать ?что (=условие ?что иметь ?что имя ?какой %C2).
		устанавливать ?что обработчик (?чего условие, ?какой Snake.СP002).
		устанавливать ?что приоритет (?чего условие, ?какой 2).
		#
		создавать ?что (=условие ?что иметь ?что имя ?какой %C3).
		устанавливать ?что обработчик (?чего условие, ?какой Snake.СP003).
		устанавливать ?что приоритет (?чего условие, ?какой 2).
		#
		создавать ?что (=условие ?что иметь ?что имя ?какой %C4).
		устанавливать ?что обработчик (?чего условие, ?какой Snake.СP004).
		устанавливать ?что приоритет (?чего условие, ?какой 2).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T1).
		регистрировать ?что триггер (?на-что изменение, ?для-чего поле).
		присоединять (?что триггер, ?к-чему %C1).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T2).
		регистрировать ?что триггер (?какой "!=", ?на-что значение ?какой 3, ?для-чего поле).
		присоединять (?что триггер, ?к-чему %C1).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T3).
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой 1, ?для-чего поле).
		устанавливать ?что обработчик (?чего триггер, ?какой Snake.TP001).
		устанавливать ?что приоритет (?чего триггер, ?какой 1).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T4).
		устанавливать ?что класс (?чего триггер, ?какой "(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой 3, ?для-чего поле).
		устанавливать ?что обработчик (?чего триггер, ?какой Snake.TP002).
		устанавливать ?что приоритет (?чего триггер, ?какой 3).
		присоединять (?что триггер, ?к-чему %C2).
		присоединять (?что триггер, ?к-чему %C3).
		присоединять (?что триггер, ?к-чему %C4).
		#
		создавать ?что (=поле ?что иметь ?что имя ?какой %направление_движения).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T5).
		устанавливать ?что класс (?чего триггер, ?какой "змейка ?что двигаться ?куда вниз.").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой %НАПРАВЛЕНИЕ_ВНИЗ, ?для-чего поле).
		присоединять (?что триггер, ?к-чему %C2).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T6).
		устанавливать ?что класс (?чего триггер, ?какой "змейка ?что двигаться ?куда вверх.").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой %НАПРАВЛЕНИЕ_ВВЕРХ, ?для-чего поле).
		присоединять (?что триггер, ?к-чему %C3).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T7).
		устанавливать ?что класс (?чего триггер, ?какой "змейка ?что двигаться ?куда влево.").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой %НАПРАВЛЕНИЕ_ВЛЕВО, ?для-чего поле).
		присоединять (?что триггер, ?к-чему %C4).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T8).
		устанавливать ?что класс (?чего триггер, ?какой "змейка ?что двигаться ?куда вправо.").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой %НАПРАВЛЕНИЕ_ВПРАВО, ?для-чего поле).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T9).
		устанавливать ?что класс (?чего триггер, ?какой "(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя верхняя).").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой 1, ?для-чего элемент (?чего %координаты_Y, ?какой 3)).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T10).
		устанавливать ?что класс (?чего триггер, ?какой "(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя нижняя).").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой 10, ?для-чего элемент (?чего %координаты_Y, ?какой 3)).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T11).
		устанавливать ?что класс (?чего триггер, ?какой "(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя левая).").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой 1, ?для-чего элемент (?чего %координаты_X, ?какой 3)).
		#
		создавать ?что (=триггер ?что иметь ?что имя ?какой %T12).
		устанавливать ?что класс (?чего триггер, ?какой "(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя правая).").
		регистрировать ?что триггер (?какой "==", ?на-что значение ?какой 20, ?для-чего элемент (?чего %координаты_X, ?какой 3)).
		#
		# Вызов временной процедуры
		#выполнять ?что (=процедура ?что иметь ?что имя ?какой Snake.Temp2).
	}

	процедура Initialize
	{
		# Инициализация при загрузке модуля
		устанавливать ?что приоритет (?чего Snake.C7, ?какой 2).
		устанавливать ?что значение (?чего %координатный_итератор, ?какой 1).
		устанавливать ?что значение (?чего %направление_движения, ?какой %НАПРАВЛЕНИЕ_ВНИЗ).
		печатать ?что значение ?какой "INIT[20:10]\n".
		печатать ?что значение ?какой "SET[A1]\n".
		печатать ?что значение ?какой "SET[A2]\n".
		печатать ?что значение ?какой "SET[A3]\n".
		печатать ?что значение ?какой "REFR[]\n".
	}

	процедура Temp1
	{
		# Временная процедура
	}

	процедура Temp2
	{
		# Временная процедура
		#создавать ?что (=условие ?что иметь ?что имя ?какой %C6).
		#устанавливать ?что обработчик (?чего условие, ?какой \"устанавливать ?что значение (?чего %направление_движения, ?какой %НАПРАВЛЕНИЕ_ВПРАВО).\").
		#
		#находить ?что (=триггер ?что иметь ?что класс ?какой "змейка ?что двигаться ?куда вниз.").
		#присоединять (?что триггер, ?к-чему %C6).
		#
		#находить ?что (=триггер ?что иметь ?что класс ?какой "(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя нижняя).").
		#присоединять (?что триггер, ?к-чему %C6).
		#
		#находить ?что (=триггер ?что иметь ?что класс ?какой "(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.").
		#присоединять (?что триггер, ?к-чему %C6).
	}

	процедура TP001
	{
		# Триггер координатный_итератор==1
		печатать ?что значение ?какой "CLR[".
		использовать ?что элемент (?чего %координаты_X, ?какой 1).
		преобразовывать (?что элемент ?как-что число, ?во-что %Temp ?как-что буква).
		печатать ?что значение ?чего %Temp.
		использовать ?что элемент (?чего %координаты_Y, ?какой %координатный_итератор).
		печатать ?что значение ?чего элемент.
		печатать ?что значение ?какой "]\n".
	}

	процедура TP002
	{
		# Триггер координатный_итератор==3
		устанавливать ?что значение (?чего %координатный_итератор, ?какой 1).
		печатать ?что значение ?какой "REFR[]\n".
	}

	процедура СP001
	{
		# Условие (координатный_итератор, координатный_итератор!=3)
		устанавливать ?что значение (?чего %Temp, ?какой %координатный_итератор).
		увеличивать (?что значение ?чего %Temp, ?на-сколько 1).
		использовать ?что элемент (?чего %координаты_X, ?какой %Temp).
		устанавливать ?что значение (?чего элемент (?чего %координаты_X, ?какой %координатный_итератор), ?какой элемент).
		использовать ?что элемент (?чего %координаты_Y, ?какой %Temp).
		устанавливать ?что значение (?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор), ?какой элемент).
		увеличивать (?что значение ?чего %координатный_итератор, ?на-сколько 1).
	}

	процедура СP002
	{
		# Условие (координатный_итератор==3, направление_движения==%НАПРАВЛЕНИЕ_ВНИЗ)
		использовать ?что элемент (?чего %координаты_X, ?какой %координатный_итератор).
		преобразовывать (?что элемент ?как-что число, ?во-что %Temp ?как-что буква).
		увеличивать (?что значение ?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор), ?на-сколько 1).
		печатать ?что значение ?какой "SET[".
		печатать ?что значение ?чего %Temp.
		печатать ?что значение ?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор).
		печатать ?что значение ?какой "]\n".
	}

	процедура СP003
	{
		# Условие (координатный_итератор==3, направление_движения==%НАПРАВЛЕНИЕ_ВВЕРХ)
		использовать ?что элемент (?чего %координаты_X, ?какой %координатный_итератор).
		преобразовывать (?что элемент ?как-что число, ?во-что %Temp ?как-что буква).
		уменьшать (?что значение ?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор), ?на-сколько 1).
		печатать ?что значение ?какой "SET[".
		печатать ?что значение ?чего %Temp.
		печатать ?что значение ?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор).
		печатать ?что значение ?какой "]\n".
	}

	процедура СP004
	{
		# Условие (координатный_итератор==3, направление_движения==%НАПРАВЛЕНИЕ_ВЛЕВО)
		уменьшать (?что значение ?чего элемент (?чего %координаты_X, ?какой %координатный_итератор), ?на-сколько 1).
		использовать ?что элемент (?чего %координаты_X, ?какой %координатный_итератор).
		преобразовывать (?что элемент ?как-что число, ?во-что %Temp ?как-что буква).
		печатать ?что значение ?какой "SET[".
		печатать ?что значение ?чего %Temp.
		печатать ?что значение ?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор).
		печатать ?что значение ?какой "]\n".
	}

	условие C7
	(
		# Условие (координатный_итератор==3, направление_движения==%НАПРАВЛЕНИЕ_ВПРАВО)
		(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.
		змейка ?что двигаться ?куда вправо.
	)
	{
		увеличивать (?что значение ?чего элемент (?чего %координаты_X, ?какой %координатный_итератор), ?на-сколько 1).
		использовать ?что элемент (?чего %координаты_X, ?какой %координатный_итератор).
		преобразовывать (?что элемент ?как-что число, ?во-что %Temp ?как-что буква).
		печатать ?что значение ?какой "SET[".
		печатать ?что значение ?чего %Temp.
		печатать ?что значение ?чего элемент (?чего %координаты_Y, ?какой %координатный_итератор).
		печатать ?что значение ?какой "]\n".
	}

	условие C8
	(
		# Условие (%координаты_Y[%координатный_итератор]==10, %координатный_итератор==3, %направление_движения==%НАПРАВЛЕНИЕ_ВНИЗ)
		змейка ?что двигаться ?куда вниз.
		(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя нижняя).
		(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.
	)
	{
		устанавливать ?что значение (?чего %направление_движения, ?какой %НАПРАВЛЕНИЕ_ВПРАВО).
	}

	условие C9
	(
		# Условие (%координаты_X[%координатный_итератор]==20, %координатный_итератор==3, %направление_движения==%НАПРАВЛЕНИЕ_ВПРАВО)
		змейка ?что двигаться ?куда вправо.
		(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя правая).
		(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.
	)
	{
		устанавливать ?что значение (?чего %направление_движения, ?какой %НАПРАВЛЕНИЕ_ВВЕРХ).
	}

	условие C10
	(
		# Условие (%координаты_Y[%координатный_итератор]==1, %координатный_итератор==3, %направление_движения==%НАПРАВЛЕНИЕ_ВВЕРХ)
		змейка ?что двигаться ?куда вверх.
		(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя верхняя).
		(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.
	)
	{
		устанавливать ?что значение (?чего %направление_движения, ?какой %НАПРАВЛЕНИЕ_ВЛЕВО).
	}

	условие C11
	(
		# Условие (%координаты_X[%координатный_итератор]==1, %координатный_итератор==3, %направление_движения==%НАПРАВЛЕНИЕ_ВЛЕВО)
		змейка ?что двигаться ?куда влево.
		(текущая ?какой, змейки ?чего) секция ?что находиться ?в-чем клетка (?чего поле, ?какой крайняя левая).
		(текущая ?какой, змейки ?чего) секция ?что быть ?чем голова.
	)
	{
		устанавливать ?что значение (?чего %направление_движения, ?какой %НАПРАВЛЕНИЕ_ВНИЗ).
	}